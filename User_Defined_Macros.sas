* FIND THE TOTAL NUMBER OF OBSERVATIONS;
%MACRO TOTAL_OBS(DSC = );
	PROC SQL;
		TITLE "TOTAL NUMBER OF OBSERVATIONS IN THE CENSUS DATASET";
		SELECT COUNT(*) AS NUM INTO : DS_SIZE
		FROM &DS_CENSUS;
	QUIT;
	%PUT &DS_SIZE;
%MEND TOTAL_OBS;

/* MODULER MACRO FOR GENERATING PIE CHART FOR DISCRETE NUMERIC VARIABLES*/
%MACRO PIECHART_NUMERIC(DSC = , VARNAME = , VARFMT = NULL_FMT, DSKIN = GLOSS, MSLICES = 6);
	PROC SGPIE DATA = &DSC;	
		FORMAT &VARNAME &VARFMT..;
		
		STYLEATTRS DATACOLORS = (RED BLUE GOLD MAROON MAGENTA ORANGE PINK ROSE STEEL TAN)
								BACKCOLOR = DEEPSKYBLUE;
		PIE &VARNAME / 	DATALABELDISPLAY = (PERCENT CATEGORY) 
						DATALABELATTRS = (COLOR = WHITE FAMILY = GRAMAND SIZE = 12 STYLE = NORMAL WEIGHT = BOLD)
						DATASKIN = &DSKIN
						MAXSLICES = &MSLICES;
	RUN;
	QUIT;
%MEND PIECHART_NUMERIC;

* GENERATE PIE CHART FOR CATEGORICAL VARIABLES;
%MACRO PIECHART_CATECARIC(DSC = , VARNAME = , VARFMT = NULL_FMT, DSKIN = GLOSS, MSLICES = 6);
	PROC SGPIE DATA = &DSC;	
		FORMAT &VARNAME $&VARFMT..;
		styleattrs datacolors=(GOLD RED GREEN  MAROON MAGENTA BLUE ORANGE PINK ROSE STEEL TAN);
		PIE &VARNAME / 	DATALABELDISPLAY = (PERCENT CATEGORY) 
						DATALABELATTRS = (COLOR = BLACK FAMILY = GRAMAND SIZE = 10 STYLE = Normal WEIGHT = Bold)
						DATASKIN = &DSKIN
						MAXSLICES = &MSLICES;
	RUN;
	QUIT;
%MEND PIECHART_CATECARIC;

* CREATE A VERTICAL BAR CHART;
%MACRO BAR_CHART_CATE(DSC = , VARNAME = , VARFMT = NULL_FMT, VAR_DESC = );
	PROC SGPLOT DATA = &DSC. PCTNDEC =2 NOWALL NOBORDER;
		STYLEATTRS BACKCOLOR = DEEPSKYBLUE;
		TITLE J = CENTER HEIGHT = 12pt COLOR = WHITE BCOLOR= DEEPSKYBLUE 
					"DISTRIBUTION OF " COLOR = BLUE "%UPCASE(&VAR_DESC.) " HEIGHT = 12pt COLOR = WHITE "GROUPS";
		
		YAXIS VALUEATTRS = (COLOR= DEEPSKYBLUE FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD)
			  DISPLAY = (NOTICKS NOLABEL NOLINE NOVALUES);

		XAXIS VALUEATTRS = (COLOR= WHITE FAMILY = GRAMAND SIZE = 8 WEIGHT = BOLD)
			  DISPLAY = (NOTICKS NOLABEL NOLINE);
	
		VBAR &VARNAME. /GROUPORDER = ASCENDING STAT = PERCENT FILLTYPE = SOLID NOOUTLINE DATALABEL COLORSTAT = PERCENT 
						colormodel=(green gold red) datalabel dataskin=pressed 
						DATALABELATTRS = (COLOR= DARKMAGENTA  FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD);
		/*XAXISTABLE &VARNAME. / LOCATION = INSIDE STAT = PERCENT NOLABEL
								VALUEATTRS = (COLOR= DARKMAGENTA  FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD);*/
		FORMAT &VARNAME. $&VARFMT..;
		RUN;
	QUIT;
%MEND BAR_CHART_CATE;


/* MODULER MACRO FOR GENERATING VERTICAL BAR CHART*/
%MACRO BAR_CHART_DISCRETE(DSC = , VARNAME = , VARFMT = NULL_FMT, VAR_DESC = );
	PROC SGPLOT DATA = &DSC. NOWALL NOBORDER PCTNDEC =2;
		STYLEATTRS BACKCOLOR = DEEPSKYBLUE;
		TITLE J = CENTER HEIGHT = 12pt COLOR = WHITE BCOLOR= DEEPSKYBLUE 
					"DISTRIBUTION OF " COLOR = BLUE "%UPCASE(&VAR_DESC.) " HEIGHT = 12pt COLOR = WHITE "GROUPS";
		YAXIS DISPLAY = (NOTICKS NOLABEL NOLINE NOVALUES)
			  VALUEATTRS = (COLOR= DEEPSKYBLUE FAMILY = GRAMAND SIZE = 14 WEIGHT = BOLD);
			  /*LABELATTRS = (COLOR= BROWN FAMILY = GRAMAND SIZE =14 WEIGHT = BOLD);*/

		XAXIS  display=(noticks nolabel noline)
			  VALUEATTRS = (COLOR= WHITE FAMILY = GRAMAND SIZE = 11 WEIGHT = BOLD);
			  /*DISPLAY = (nolabel);*/

		STYLEATTRS DATACOLORS = (GOLD RED GREEN MAROON MAGENTA BLUE ORANGE);	
		VBAR &VARNAME. /GROUPORDER = ASCENDING STAT = PERCENT FILLTYPE = SOLID NOOUTLINE DATALABEL COLORSTAT = PERCENT 
						COLORMODEL = (GOLD GREEN RED) DATASKIN = PRESSED
						DATALABELATTRS = (COLOR= DARKMAGENTA  FAMILY = GRAMAND SIZE = 11 WEIGHT = BOLD);

		/*XAXISTABLE &VARNAME. / LOCATION = INSIDE STAT = PERCENT NOLABEL 
								VALUEATTRS = (COLOR= DARKMAGENTA  FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD);*/

		FORMAT &VARNAME. &VARFMT..;
		RUN;
	QUIT;
%MEND BAR_CHART_DISCRETE;





* CHECK THE AGE CRETERIA FOR ALL VARIABLES;
%MACRO CHECK_AGE_LIMIT(DSC = , CVARS = CHAR_ONLY, NVARS = NUM_ONLY);
	%LET N = %SYSFUNC(COUNTW(&CVARS));
	%DO I = 1 %TO &N;
		%LET CVAR_NAME = %SCAN(&CVARS, &I);
		TITLE "COUNT BY %UPCASE(&CVAR_NAME)";

		PROC SQL;
			SELECT COUNT(*) AS FREQ
			FROM &DSC
			WHERE &CVAR_NAME EQ 'A';
		QUIT;
	%END;

	%LET N = %SYSFUNC(COUNTW(&NVARS));
	%DO I = 1 %TO &N;
		%LET NVAR_NAME = %SCAN(&NVARS, &I);
		TITLE "COUNT BY %UPCASE(&NVAR_NAME)";

		PROC SQL;
			SELECT COUNT(*) AS FREQ
			FROM &DSC
			WHERE PUT(&NVAR_NAME, BEST12.) EQ 'A';
		QUIT;
	%END;
%MEND CHECK_AGE_LIMIT;


* DROP THE OBSERVATIONS THAT CONTAIN UNKNOWN DATA VALUES SUCH AS 'A', 'D', 'N', 'R' AND 'S'.;
%MACRO DROP_UNKNOWN_VAL_OBS(DSC = , CVARS = ); 
	%LET N = %SYSFUNC(COUNTW(&CVARS));
	%DO I = 1 %TO &N;
		%LET VAR_NAME = %SCAN(&CVARS, &I);
		TITLE "COUNT BY %UPCASE(&VAR_NAME)";

		PROC SQL;
			DELETE 
			FROM &DSC
			WHERE PRXMATCH("!.*[aAdDnNrRsS].*!", &VAR_NAME);
		QUIT;

		PROC SQL;
			SELECT &VAR_NAME, COUNT(*) AS NUM
			FROM &DSC
			GROUP BY &VAR_NAME;
		QUIT;
	%END;
%MEND DROP_UNKNOWN_VAL_OBS;



%MACRO PROC_MEANS(DSC = , NVARS = );
	%LET N = %SYSFUNC(COUNTW(&NVARS));
	%DO I = 1 %TO &N;
		%LET NVAR_NAME = %SCAN(&NVARS, &I);
		TITLE "MEANS OF %UPCASE(&NVAR_NAME.)";
		PROC MEANS DATA = &DSC MAXDEC = 2 N NMISS MIN MEAN STD MAX RANGE;
			VAR &NVAR_NAME;
		RUN;	
	%END;
%MEND PROC_MEANS;


* MODULAR MACRO FOR UNIVARIATE ANALYSIS FOR CHARACTER VARIABLES ;
%MACRO UNI_ANALYSIS_CATE(DSC = , CVARS = , FMT = , DATASKIN = );
	%LET VN= %SYSFUNC(COUNTW(&CVARS));
	%LET FN = %SYSFUNC(COUNTW(&FMT));
	%LET DN = %SYSFUNC(COUNTW(&DATASKIN));

	%IF(&VN EQ &FN) %THEN 
	%DO;
		TITLE "UNIVARIATE DESCRIPTIVE ANAYSIS OF ALL CATEGORICAL VARABILES:SUMMARY";
		%DO I = 1 %TO &VN;
			%LET CVAR_NAME = %SCAN(&CVARS, &I);
			%LET CVAR_FMT = %SCAN(&FMT, &I);
			%LET DATA_SKIN = %SCAN(&DATASKIN, &I);

			TITLE "UNIVARIATE DESCRIPTIVE ANALYSIS: %UPCASE(&CVAR_NAME)";

			PROC SQL;
				SELECT COUNT(*) INTO : NUM_OBS
				FROM &DSC;	
			QUIT;

			PROC SQL;
				SELECT &CVAR_NAME FORMAT $&CVAR_FMT.., COUNT(*) AS FREQ, PUT((COUNT(*) / &NUM_OBS), PERCENT8.2) AS Percent
				FROM &DSC
				GROUP BY &CVAR_NAME
				ORDER BY FREQ DESC;
			QUIT;

			%PIECHART_CATECARIC(DSC = &DSC., VARNAME = &CVAR_NAME., VARFMT = &CVAR_FMT., DSKIN = &DATA_SKIN., MSLICES = 8);
			
			/*%BAR_CHART(DSC = &DSC., VARNAME = &CVAR_NAME., VARFMT = &CVAR_FMT., VAR_DESC = &CVAR_NAME.)*/
		%END;
	%END;
%MEND UNI_ANALYSIS_CATE;



* UNIVARIATE ANALYSIS FOR NUMERICAL VARIABLES;
%MACRO UNI_ANALYSIS_NUMERIC(DSC = , NVARS = );
	TITLE "DISTRIBUTION OF ALL NUMERIC VARABILES: SUMMARY";

	PROC MEANS DATA = &DSC N NMISS MIN Q1 Q3 MEAN MEDIAN MAX;
	 VAR &NVARS;
	RUN;

	%LET N = %SYSFUNC(COUNTW(&NVARS));
	%DO I = 1 %TO &N;
	  	%LET NVAR = %SCAN(&NVARS, &I);
		TITLE "DISTRIBUTION OF &NVAR.: HISTOGRAM AND DENSITY CURVE";

		PROC MEANS DATA = &DSC. MAXDEC = 2 N NMISS MIN MEAN STD MAX RANGE;
			VAR &NVAR.;
		RUN;

		PROC UNIVARIATE DATA = &DSC.;
			VAR &NVAR. ;
		RUN;

		PROC SGPLOT DATA = &DSC.;
	 		HISTOGRAM &NVAR.;
	 		DENSITY &NVAR./ TYPE = KERNEL;
	 		KEYLEGEND / LOCATION = INSIDE POSITION = TOPRIGHT ACROSS=1 NOBORDER;
		RUN;
		QUIT;

		*TITLE "DISTRIBUTION OF &NVAR. : VERTICAL BOX-PLOT";
		PROC SGPLOT DATA = &DSC. NOWALL NOBORDER;
			STYLEATTRS DATACOLORS = (GOLD) BACKCOLOR = DEEPSKYBLUE;
		 	VBOX &NVAR./ DATASKIN = SHEEN DATALABEL = &NVAR. FILLATTRS = (COLOR = GREEN TRANSPARENCY = .2) FILL NOTCHES;
			YAXIS DISPLAY = (NOLABEL) ;
		 	XAXIS DISPLAY = (NOLABEL);
		RUN;
		QUIT;
		/*PROC SGPLOT DATA = &DSC;
	 		VBOX &NVAR.;
			YAXIS GRID;
	 		XAXIS DISPLAY = (NOLABEL);
		RUN;
		QUIT;*/
	%END;
%MEND UNI_ANALYSIS_NUMERIC;

* PERFORM CHI-SQUARE TEST BETWEEN TARGET AND INDEPENDENT VARIABLES (WITH STACKED GRAPH);
%MACRO CHISQUARE_TEST(DSC = , CVARS = , FMT = , TARVAR = &TARGET_VAR);
	%LET VN= %SYSFUNC(COUNTW(&CVARS));
	%LET FN = %SYSFUNC(COUNTW(&FMT));
	
	%IF(&VN EQ &FN) %THEN 
	%DO;
		TITLE "CHISQUARE TEST BETWEEN TARGET AND INDEPENDENT EACH VARIABLE: SUMMARY";
		%DO I = 1 %TO &VN;
			%LET INDVAR = %SCAN(&CVARS, &I);
			%LET CVAR_FMT = %SCAN(&FMT, &I);

			TITLE "CHISQUARE TEST BETWEEN  AND %";

			PROC FREQ DATA = &DSC;
				TITLE "RELATIONSHIP BETWEEN %UPCASE(&TARVAR) AND  %UPCASE(&INDVAR.)";
				TABLE &TARVAR. * &INDVAR. / CHISQ NOROW NOCOL;
				FORMAT  &TARVAR. PRN_GRP_FMT.;
			RUN;

			PROC SORT DATA = &DS_CENSUS OUT = KGS.CENTMP;
				BY &TARVAR.; 
			RUN;

			PROC FREQ DATA = KGS.CENTMP;
				BY &TARVAR.;
				TABLE &INDVAR. / OUT = KGS.CENTMPFREQ; 
				FORMAT  &TARVAR. PRN_GRP_FMT.;
			RUN;

			PROC SQL;
				ALTER TABLE KGS.CENTMPFREQ
				ADD PP NUM(8);
			QUIT;

			PROC SQL;
				UPDATE KGS.CENTMPFREQ
				SET PP = PERCENT/100.0;
			QUIT;

			/* PROC PRINT DATA = KGS.CENTMPFREQ(OBS=24);RUN; */

			PROC SGPLOT DATA = KGS.CENTMPFREQ NOWALL NOBORDER ;
				TITLE J = CENTER HEIGHT = 12pt COLOR = WHITE BCOLOR= DEEPSKYBLUE 
					"RELATIONSHIP BETWEEN " COLOR = BLUE "%UPCASE(&TARVAR.) " HEIGHT = 12pt COLOR = WHITE " AND " COLOR = BLUE "%UPCASE(&INDVAR.)";
				STYLEATTRS BACKCOLOR = DEEPSKYBLUE;

				YAXIS VALUEATTRS = (COLOR= BLACK FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD)
						LABELATTRS = (COLOR= BLACK FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD)
						DISPLAY = (NOTICKS  NOLINE NOVALUES)					
						LABEL = "PERCENT OF WAGE EARNINGS FREQUENCY";

				XAXIS VALUEATTRS = (COLOR= BLACK FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD)
					  	DISPLAY = (NOTICKS NOLABEL NOLINE)
						DISCRETEORDER = DATA;
				FORMAT PP PERCENT10.2;
				VBAR &TARVAR. / RESPONSE = PP GROUP=&INDVAR. GROUPDISPLAY = STACK
								DATALABEL DATALABELATTRS = (SIZE = 11 WEIGHT = BOLD)
								SEGLABEL SEGLABELATTRS = (SIZE = 8 );
				KEYLEGEND / LOCATION = OUTSIDE POSITION = RIGHT ACROSS = 1 NOBORDER;
				FORMAT &TARVAR. PRN_GRP_FMT. &INDVAR. $&CVAR_FMT..;
			RUN;
			QUIT;			
		%END;
	%END;
%MEND CHISQUARE_TEST;

* PERFORM CHI-SQUARE TEST BETWEEN TARGET AND INDEPENDENT VARIABLES (NON STACKED GRAPH);
%MACRO CHISQUARE_TEST_NS(DSC = , CVARS = , FMT = , TARVAR = &TARGET_VAR);
	%LET VN= %SYSFUNC(COUNTW(&CVARS));
	%LET FN = %SYSFUNC(COUNTW(&FMT));
	
	%IF(&VN EQ &FN) %THEN 
	%DO;
		TITLE "CHISQUARE TEST BETWEEN TARGET AND INDEPENDENT EACH VARIABLE: SUMMARY";
		%DO I = 1 %TO &VN;
			%LET INDVAR = %SCAN(&CVARS, &I);
			%LET CVAR_FMT = %SCAN(&FMT, &I);

			TITLE "CHISQUARE TEST BETWEEN  AND %";

			PROC FREQ DATA = &DSC;
				TITLE "RELATIONSHIP BETWEEN %UPCASE(&TARVAR) AND  %UPCASE(&INDVAR.)";
				TABLE &TARVAR. * &INDVAR. / CHISQ NOROW NOCOL;
				FORMAT  &TARVAR. PRN_GRP_FMT.;
			RUN;

			PROC SGPLOT DATA = &DS_CENSUS. NOWALL NOBORDER;
				TITLE J = CENTER HEIGHT = 12pt COLOR = WHITE BCOLOR= DEEPSKYBLUE 
					"RELATIONSHIP BETWEEN " COLOR = BLUE "%UPCASE(&TARVAR.) " HEIGHT = 12pt COLOR = WHITE " AND " COLOR = BLUE "%UPCASE(&INDVAR.)";
				STYLEATTRS BACKCOLOR = DEEPSKYBLUE;
				
				YAXIS VALUEATTRS = (COLOR= BLACK FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD)
					  DISPLAY = (NOTICKS NOLINE NOVALUES)
					  LABEL = "PERCENT OF WAGE EARNINGS FREQUENCY"
					  LABELATTRS = (COLOR= BLACK FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD);

				XAXIS VALUEATTRS = (COLOR= BLACK FAMILY = GRAMAND SIZE = 12 WEIGHT = BOLD)
					  DISPLAY = (NOTICKS NOLABEL NOLINE);
				VBAR &TARVAR./ GROUP = &INDVAR. DATALABEL STAT = PERCENT
								DATALABELATTRS = (COLOR= BLACK  FAMILY = GRAMAND SIZE = 11 WEIGHT = BOLD);

				KEYLEGEND / LOCATION = INSIDE POSITION = TOP ACROSS = 1 NOBORDER;
				FORMAT &TARVAR. PRN_GRP_FMT. &INDVAR. $&CVAR_FMT..;
			RUN;
			QUIT; 			
		%END;
	%END;
%MEND CHISQUARE_TEST_NS;


* PERFORM CORRELATION TEST BETWEEN TARGET AND INDEPENDENT VARIABLES;
%MACRO CORRELATION_TEST(DSC = , NVARS = , TARVAR = &TARGET_VAR);
	%LET VN= %SYSFUNC(COUNTW(&NVARS));
	
	TITLE "CORRELATION TEST BETWEEN TARGET AND INDEPENDENT EACH VARIABLE: SUMMARY";
	%DO I = 1 %TO &VN;
		%LET INDVAR = %SCAN(&NVARS, &I);
		%IF &TARVAR. NE &INDVAR. %THEN
		%DO;
			PROC CORR DATA = &DSC PLOTS = MATRIX(HISTOGRAM) PLOTS(MAXPOINTS = 100000);
				TITLE "CORRELATION BETWEEN %UPCASE(&TARVAR.) AND  %UPCASE(&INDVAR.)";
				VAR &INDVAR.;
				WITH &TARVAR. ;
			RUN;
			QUIT;	
		%END;
	%END;	
%MEND CORRELATION_TEST;

* PERFORM ANOVA TEST BETWEEN GROUPED TARGET AND INDEPENDENT  VARIABLES;
%MACRO ANOVA_TEST(DSC = , NVARS = , FMT = , TARVAR = &TARGET_VAR);
	TITLE "ANOVA TEST BETWEEN GROUPED TARGET AND NUMERICAL VARIABLES";
	%LET N = %SYSFUNC(COUNTW(&NVARS));
		
	%DO I = 1 %TO &N;
	  	%LET NVAR = %SCAN(&NVARS, &I);
		%IF &TARVAR. NE &NVAR. %THEN
		%DO;
			TITLE "ANOVA TEST BETWEEN %UPCASE(&TARVAR) AND %UPCASE(&NVAR)";

			PROC ANOVA DATA = &DSC PLOTS() PLOTS(MAXPOINTS = NONE);
				CLASS &TARVAR.;
				MODEL &NVAR. = &TARVAR.;
				MEANS &TARVAR./SCHEFFE;
				FORMAT &TARVAR. &FMT..;
			RUN;
			QUIT;
		%END;
	%END;
%MEND ANOVA_TEST;


* GENERATE NORMAL CURVES TO CHECK WHETHER THEY ARE FOLLOWING NORMAIL DISTRIBUTION OR NOT;
%MACRO CHECK_NORMAL_DISTN(DSC = , VARS = );
	%LET N = %SYSFUNC(COUNTW(&VARS));
	TITLE "QQPLOT";
	%DO I = 1 %TO &N;
		%LET NVAR = %SCAN(&VARS, &I);
		PROC UNIVARIATE DATA = &DSC NORMAL PLOT;
			VAR &NVAR;
			QQPLOT &NVAR /NORMAL(MU = EST SIGMA = EST COLOR = RED L = 1);
		RUN; 
	%END;
%MEND CHECK_NORMAL_DISTN;


* STANDARDIZE THE MISSING VALUES FOR THE NUMERIC VARIABLES;
%MACRO STANDADIZE_MISSING_VAR(DSC = , MVARS = , STDMODE = );
	%LET MN = %SYSFUNC(COUNTW(&MVARS));
	%LET SN = %SYSFUNC(COUNTW(&STDMODE));

	%DO I = 1 %TO &MN;
		%IF &MN. EQ &SN. %THEN
		%DO;
			%LET MVAR = %SCAN(&MVARS., &I);
			%LET SMODE = %SCAN(&STDMODE., &I);
			PROC STDIZE DATA = &DSC OUT = &DSC METHOD= &SMODE REPONLY;
		 		VAR &MVAR;
			RUN;
		%END;
	%END;
%MEND STANDADIZE_MISSING_VAR;


* FIND THE MODE VALUE;
%MACRO MODE_VALUE(DSC = , CVAR = );
	PROC SQL;
		CREATE VIEW GROUPED_EDU AS
			SELECT &CVAR, COUNT(*) AS FREQ
			FROM &DSC
			GROUP BY &CVAR;

		SELECT * FROM GROUPED_EDU;

		SELECT &CVAR INTO : MODE_VAL
		FROM GROUPED_EDU
		WHERE FREQ IN (
			SELECT MAX(FREQ)
			FROM GROUPED_EDU);
		
		UPDATE &DSC
		SET &CVAR = PUT(&MODE_VAL, 2.)
		WHERE &CVAR NOT IN ('10', '11', '12', '13', '14', '15', '16', '17', '18', '19', '20');
	QUIT;
%MEND MODE_VALUE;

* DROP OUTLIERS;
%MACRO DROP_OUTLIER(DSC = , LIBNAME = , NVAR = , THRESHOLD = 1.5);
	PROC MEANS DATA = &DSC MAXDEC=2 N P25 P75 QRANGE;
	  	VAR &NVAR;
		OUTPUT Out= &LIBNAME.TEMP P25= Q1 P75=Q3 QRANGE=IQR;
	RUN;
	
	DATA &LIBNAME.TEMP;
		SET &LIBNAME.TEMP;
		LOWER_LIMIT = Q1- (&THRESHOLD * IQR);
		UPPER_LIMIT = Q3 +(&THRESHOLD * IQR);
	RUN;

	PROC SQL;
	 CREATE Table &LIBNAME.TEMP_02 AS
		 SELECT A.*,B.LOWER_LIMIT,B.UPPER_LIMIT
		 FROM &DSC AS A , &LIBNAME.TEMP AS B;
	 QUIT;

	 DATA &LIBNAME.TEMP_03;
	 	SET &LIBNAME.TEMP_02;
		IF &NVAR LE LOWER_LIMIT THEN RANGE = "BELOW LOWER LIMIT";
	 	ELSE IF &NVAR GE UPPER_LIMIT THEN RANGE = "ABOVE UPPER LIMIT";
	 	ELSE  RANGE = "WITHIN RANGE";
	RUN;

	PROC SQL;
	 CREATE Table &LIBNAME.TEMP_04 AS
		 SELECT *
		 	FROM &LIBNAME.TEMP_03
		 WHERE RANGE = "WITHIN RANGE";
	QUIT;
	
	PROC Sql;
		SELECT Count(*) INTO : oriobs 
		FROM &DSC;
	QUIT;

	PROC Sql;
		SELECT Count(*) INTO : updateobs
		FROM &LIBNAME.TEMP_04;
	QUIT;	

	PROC DELETE DATA = &DSC; RUN;
	
	DATA &DSC;
		SET &LIBNAME.TEMP_04 (DROP = LOWER_LIMIT UPPER_LIMIT RANGE);
	RUN;

	* Drop the temporary datasets;
	PROC DELETE DATA = &LIBNAME.TEMP; Run;
	PROC DELETE DATA = &LIBNAME.TEMP_02; Run;
	PROC DELETE DATA = &LIBNAME.TEMP_03; Run;
	PROC DELETE DATA = &LIBNAME.TEMP_04; Run;
%MEND DROP_OUTLIER;




